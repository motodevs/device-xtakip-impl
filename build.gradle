group 'com.openvehicletracking'
version '2.1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'


sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'com.openvehicletracking:core:2.1.0-SNAPSHOT'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'OpenVehicleTracking tracking device protocols'
                packaging 'jar'
                description 'tracking device message parsers and device implementations'
                url 'http://openvehicletracking.com'

                scm {
                    connection 'scm:git:https://github.com/openvehicletracking/protocols.git'
                    developerConnection 'scm:git:https://github.com/openvehicletracking/protocols.git'
                    url 'https://github.com/openvehicletracking/protocols'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/openvehicletracking/protocols/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'oksuz'
                        name 'Yunus Oksuz'
                        email 'yunusoksuz@gmail.com'
                    }
                }
            }
        }
    }
}